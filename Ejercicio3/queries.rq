# ----------------------- Consulta 1 -----------------------
# Número de arboles en mal estado en la calle y en los parques
PREFIX tree: <http://example.org/tree/>
SELECT 
	?type (COUNT(?tree) AS ?numTreesPoorCondition)
WHERE {
	?tree tree:condition "Poor" ;
	      tree:type ?type .
}
GROUP BY ?type


# ----------------------- Consulta 2 -----------------------
# Media de altura, ancho y radio de extensión por especie (dbpedia) de árbol:
PREFIX schema: <http://schema.org/>
PREFIX tree: <http://example.org/tree/>
PREFIX dbr: <http://dbpedia.org/resource/> 
SELECT ?species (AVG(?diameter) AS ?avgDiameter) (AVG(?height) AS ?avgHeight) (AVG(?spreadRadius) AS ?avgSpreadRadius) WHERE {
  ?tree a ?species ;
        tree:diameter ?diameter ;
        schema:height ?height ;
        tree:spreadRadius ?spreadRadius .
        
} GROUP BY ?species


# ----------------------- Consulta 3 -----------------------
# Tipo de specie de árbol (wikidata) más común en las calles
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX tree: <http://example.org/tree/>
SELECT ?species (COUNT(?streetTree) AS ?count)
WHERE {
  ?streetTree tree:type "StreetTree" ;
              wdt:P31 ?species .
}
GROUP BY ?species
ORDER BY DESC(?count)
LIMIT 1


# ----------------------- Consulta 4 -----------------------
# Árbol más alto de todos con su especie y ubicación (el dato original seguramente sea erróneo ya que el resultado son más de 800 metros)
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX tree: <http://example.org/tree/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 

SELECT ?tree ?speciesType ?species ?treeHeight ?longitude ?latitude
WHERE {
  ?tree schema:height ?treeHeight ;
        geo:long ?longitude ;
        geo:lat ?latitude .
  OPTIONAL {?tree wdt:P31 ?speciesType}
  OPTIONAL {?tree a ?species }
        
  {
    SELECT (MAX(?height) AS ?maxHeight)
    WHERE {
      ?t schema:height ?height .
    }
  }
  FILTER(?treeHeight = ?maxHeight)
}



# ----------- CONSULTAS COMBINADAS CON WIKIDATA ------------
# ----------------------- Consulta 5 -----------------------
# Número de árboles de cada tipo de especie junto con su etiqueta de Wikidata en inglés
# Tarda unos 15 minutos en ejecutarse
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX tree: <http://example.org/tree/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?speciesType ?label (COUNT(?tree) AS ?numTrees)
WHERE {
  ?tree wdt:P31 ?speciesType .
  SERVICE <https://query.wikidata.org/sparql> {
    ?speciesType rdfs:label ?label .
    FILTER(LANG(?label) = "en")
  }
}
GROUP BY ?speciesType ?label


# ----------------------- Consulta 6 ----------------------
# Para cada árbol obtener una única foto (si la tuviera) de su tipo de especie en Wikidata 
# Tarda unos 20 minutos en ejecutarse
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?tree ?label (SAMPLE(?image) AS ?sampleImage)
WHERE {
  ?tree wdt:P31 ?speciesType .
  SERVICE <https://query.wikidata.org/sparql> {
    ?speciesType rdfs:label ?label .
    FILTER(LANG(?label) = "en")
    OPTIONAL {?speciesType wdt:P18 ?image}
  }
}
group by ?tree ?label